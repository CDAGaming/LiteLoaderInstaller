buildscript {
    repositories {
		jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:2.4.2'
    }
}

apply plugin: 'java'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'com.github.johnrengelman.shadow'

// createExe dependsOn doesn't work
defaultTasks 'prepareManifest', 'build', 'createExe', 'gather'


group               = 'com.mumfrey'
ext.revision        = project.verRevision == "0" ? "" : ".${project.verRevision}"
version             = "${project.verMajor}.${project.verMinor}${project.revision}${project.verRelease}"
ext.mcversion       = "${project.verMajor}.${project.verMinor}${project.revision}"
ext.msversion       =  "${project.verMajor}.${project.verMinor}.${project.verRevision}.${project.verBuild}"
archivesBaseName    = 'liteloader-installer'
targetCompatibility = '1.8'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:21.0'
    compile 'net.sourceforge.argo:argo:4.6'
}

// resolve problem with shadowJar
task wrapJar(type: Jar) {
    baseName = 'artefacts-wrapped'
    entryCompression ZipEntryCompression.STORED
    from file(Eval.x(project, project.upstreamJar))
    destinationDir = temporaryDir
}

jar {
    destinationDir = temporaryDir
    classifier = "core"
}

shadowJar {
    from wrapJar
    classifier = 'SNAPSHOT'
}

task prepareManifest(type: Copy) {
    from 'launch4j/liteloader_installer_manifest.xml'
    into tasks.launch4j.temporaryDir
    expand(
        artefact:  project.sxsname,
        msversion: project.msversion
    )
}

createExe {
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar              = 'lib/' + project.tasks.shadowJar.archiveName
    icon             = "${projectDir}/launch4j/liteloader_installer_icon.ico"
    manifest         = "${tasks.launch4j.temporaryDir}/liteloader_installer_manifest.xml"
    outfile          = tasks.shadowJar.archiveName - ".jar" + ".exe"
    version          = project.msversion
    internalName     = project.sxsname
    productName      = "LiteLoader"
    fileDescription  = "LiteLoader Installer for Minecraft ${project.mcversion} release {project.verRelease}"
    copyright        = "Copyright (C) 2013-2018 Adam Mummery-Smith"
}

task gather(type: Copy) {
    dependsOn 'launch4j'
    from 'build/launch4j/' + launch4j.outfile
    into 'build/libs/'
}

jar.manifest.mainAttributes (
    "Main-Class":             "com.mumfrey.liteloader.installer.Installer",
    "Class-Path":             ".",
    "Built-By":               System.properties['user.name'],
    "Created-By":             System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
    "Implementation-Title":   name,
    "Implementation-Version": version
)

artifacts {
    archives jar
    archives shadowJar
}
